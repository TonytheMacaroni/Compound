plugins {
    id 'java-library'
    id 'maven-publish'

    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'io.freefair.lombok' version '6.0.0-m2'
}

group = 'com.tonythemacaroni'
version = '0.1.0'

repositories {
    mavenLocal()

    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    maven { url = uri('https://oss.sonatype.org/content/groups/public/') }
    maven { url = uri('https://repo.maven.apache.org/maven2/') }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    shadow 'io.github.classgraph:classgraph:4.8.108'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    configurations = [project.configurations.shadow]
    minimize()

    relocate 'io.github.classgraph', 'com.tonythemacaroni.compound.shaded.io.github.classgraph'
    relocate 'nonapi.io.github.classgraph', 'com.tonythemacaroni.compound.shaded.nonapi.io.github.classgraph'

    archiveClassifier.set('')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'Compound'
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
